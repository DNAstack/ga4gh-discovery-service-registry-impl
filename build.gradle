plugins {
	id 'java'
	id "org.springframework.boot" version "2.1.0.RELEASE"
	id "io.spring.dependency-management" version "1.0.6.RELEASE"
	id 'io.franzbecker.gradle-lombok' version '1.8'
    id "com.ewerk.gradle.plugins.querydsl" version "1.0.10"
}

group 'com.dnastack.discovery.registry'

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
	mavenCentral()
}

ext {
	injectVersion = "1"
	restAssuredVersion = "3.1.1"
    embeddedMongoVersion = "2.1.1"
    queryDslVersion = "4.2.1"
}

springBoot {
	buildInfo()
}

lombok {
	version = "1.18.4"
	sha256 = ""
}

sourceSets {
    main {
        java {
            srcDir "${buildDir}/generated-sources/querydsl"
        }
    }
}

querydsl {
    querydslSourcesDir = "${buildDir}/generated-sources/querydsl"
    library = "com.querydsl:querydsl-apt:${queryDslVersion}"
    jpa = true
}

project.afterEvaluate {
    project.tasks.compileQuerydsl.options.compilerArgs = [
            "-proc:only",
            "-processor", project.querydsl.processors() + ',lombok.launch.AnnotationProcessorHider$AnnotationProcessor'
    ]
}

dependencies {
	compile 'org.springframework.boot:spring-boot-starter'
	compile 'org.springframework.boot:spring-boot-starter-web'
	compile "org.springframework.boot:spring-boot-starter-actuator"
	compile "org.springframework.boot:spring-boot-starter-json"
    compile "org.springframework.boot:spring-boot-starter-data-jpa"

	compile "javax.inject:javax.inject:${injectVersion}"
    compile "com.querydsl:querydsl-jpa:${queryDslVersion}"
    compile "com.h2database:h2"

    testCompile "org.springframework.boot:spring-boot-starter-test"
	testCompile "org.springframework.security:spring-security-test"
    testCompile "com.h2database:h2"
	testCompile "io.rest-assured:rest-assured:${restAssuredVersion}"
	testCompile "io.rest-assured:json-path:${restAssuredVersion}"
}

test {
	useJUnit {
		exclude '**/*IT.class'
	}
}

task unitTest(type: Test, description: 'Runs the unit tests.', group: 'Verification') {
	useJUnit{
		include '**/*Test.class'
	}
}

task integrationTest(type: Test, description: 'Runs the integration tests', group: 'Verification') {
	useJUnit{
		include '**/*IT.class'
	}
	ignoreFailures true
}